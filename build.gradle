apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.dongbat'
version = '1.0.0'

sourceCompatibility = '1.6'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src/" ]

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      // POM signature
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      // Target repository
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUser, password: ossrhPassword)
      }
      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(userName: ossrhUser, password: ossrhPassword)
      }  

      pom.project {
        name project.name
        description project.description
        packaging 'jar'
        url 'https://github.com/implicit-invocation/jbump'

        scm {
          connection 'scm:git:https://github.com/implicit-invocation/jbump.git'
          developerConnection 'scm:git:git@github.com:implicit-invocation/jbump.git'
          url 'https://github.com/implicit-invocation/jbump.git'
        }

        licenses {
          license {
            name 'Apache License'
            url 'http://www.apache.org/licenses/'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id = 'callback'
            name = 'Duong Thanh Dat'
            email = 'tao@dongbat.com'
          }
        }
      }
    }
  }
}
